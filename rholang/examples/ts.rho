new rl(`rho:registry:lookup`), RevVaultCh, stdout(`rho:io:stdout`), log in {
  rl!(`rho:id:1o93uitkrjfubh43jt19owanuezhntag5wh74c6ur5feuotpi73q8z`, *RevVaultCh) |
  for (@(_, RevVault) <- RevVaultCh) {

    log!(("3.transfer_funds.rho")) |

    // REPLACE THE REV ADDRESSES HERE vvv
    match (
      "11112vE2RTY9rQ7bncZp3iT9bSw1PqEf3GCsQD4E2BVJ54XTwkKoE6",
      "1111Gqe4NL9eyhS7kD7qeA8nTYWGUVBNjz6oVDrQJCHexkwvrYcbC",
      2
    ) {
      (from, to, amount) => {

        new vaultCh, revVaultkeyCh in {
          @RevVault!("findOrCreate", from, *vaultCh) |
          @RevVault!("deployerAuthKey", *revVaultkeyCh) |
          for (@(true, vault) <- vaultCh; key <- revVaultkeyCh) {

            log!(("Nutzipper sending via node2.testnet.rchain-dev.tk at 04.20.2019-19:50:16 UTC ", amount, "REV from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {

                log!(("Finished transfer of ", amount, "REV to", to, "result was:", result))
              }
            }
          }
        }
      }
    }
  } |

  contract log(@data) = {
    @"DEMO"!(data) | stdout!(data)
  }
}